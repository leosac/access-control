# Maintainer: Thibault 'Ryp' Schueller  <ryp.sqrt@gmail.com>

_gitname=leosac
pkgname=leosac-git
pkgver=0.3.0.r11.g7904443
pkgrel=1
pkgdesc="Open source access control"
arch=('i686' 'x86_64' 'armv6h')
url="https://github.com/islog/leosac"
license=('AGPL3')
makedepends=('git cmake boost tclap')
conflicts=('leosac')
provides=('leosac')
install='leosac.install'
source=('git+https://github.com/islog/leosac.git'
    leosacd.service
    leosac.install)
sha512sums=('SKIP'
    'e459abfd8dec780fbacd77df9a273a1ea95bb9c323a0666113281fb5534c7b45a0e7b5ccd17c484d8c4e4518e7fe4617aa73887de892efa57a38540a43bc1e66'
    '53a386f33283f7531d1ba0274d8520c8ed9e6bb04b6be45197e4e940ac33240ca27140450b52d8e8a2db3b04b99ce7b306dd095152e5a797c66b974968c3210a')

pkgver() {
    cd "$srcdir/$_gitname"
    git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
    mkdir -p build
    pushd build
    cmake "$srcdir/$_gitname"                   \
        -DCMAKE_BUILD_TYPE=Release              \
        -DCMAKE_INSTALL_PREFIX="$pkgdir/usr"    \
        -DLEOSAC_PLATFORM=None
    make
}

package() {
    pushd build
    # Install leosac
    make install
    # Install service file
    install -m 755 -d "$pkgdir/usr/lib/systemd/system"
    install -m 644 -t "$pkgdir/usr/lib/systemd/system"  \
        "$srcdir/leosacd.service"
    # Deamon configuration folder
    install -m 755 -d "$pkgdir/etc/leosac.d"
    install -m 644 -t "$pkgdir/etc/leosac.d"            \
        "$pkgdir/usr/share/leosac/cfg/empty/core.xml"   \
        "$pkgdir/usr/share/leosac/cfg/empty/hw.xml"
}

